apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

buildscript {
  	repositories {
		jcenter()
  	}
  	dependencies {
    		classpath 'org.akhikhl.gretty:gretty:+'
  	}
}


dependencies {
	compile project(':core')
	
	// compile each subproject
	file('subprojects').eachDir() { dir -> 
	    if (dir.name.startsWith("extension-")) {
	    		compile project(':' + dir.name)
	    }
	}
}


allprojects {
    repositories {
        jcenter()
        mavenCentral()
    }
}


// Applies to all subprojects
subprojects {
	
	apply plugin: 'java-library'
	
    version = '1.0'
    
    dependencies {
    		// Jersey and Servlet
    		compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version:'2.26'
		compile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.0.1'
		compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version:'2.26'		
	}
}


// Instructions to build .war file
war {
    archiveName 'jersey-gradle-multiproject.war'
    
    
    // Put compiled files of core into war
    from (project(':core').sourceSets.main.output.classesDir) {
		into 'WEB-INF/classes' 
	}
    
    
    // Put compiled files of each subproject into war
	file('subprojects').eachDir() { dir -> 
	    if (dir.name.startsWith("extension-")) {
	    		from (project(dir.name).sourceSets.main.output.classesDir) {
	    			into 'WEB-INF/classes'
	    		}
    		}
	}
}


// Embedded Webserver
gretty {
	httpPort = 8081
	servletContainer = 'jetty9'
	contextPath = '/'
}